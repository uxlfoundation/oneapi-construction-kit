if(NOT DEFINED GENERATE_OUTPUT_FILE)
  message(FATAL_ERROR
    "Required cmake variable GENERATE_OUTPUT_FILE not set!"
  )
endif()

# Disable warnings about empty list items. cmake before 2.4 didn't handle these well.
cmake_policy(SET CMP0007 NEW)

set(generate_input_definitions "")

file(READ "${CMAKE_CURRENT_SOURCE_DIR}/main.in" main)

set(types "char;uchar;short;ushort;int;uint;long;ulong;half;float;double")
set(small "void;void;char;uchar;short;ushort;int;uint;void;half;float")
set(large "short;ushort;int;uint;long;ulong;void;void;float;double;void")
set(signed "char;char;short;short;int;int;long;long;short;int;long")
set(unsigned "uchar;uchar;ushort;ushort;uint;uint;ulong;ulong;ushort;uint;ulong")
set(is_signed "true;false;true;false;true;false;true;false;true;true;true")
set(is_int "true;true;true;true;true;true;true;true;false;false;false")
set(min "0x80;0u;0x8000;0u;0x80000000;0u;0x8000000000000000;0u;ABACUS_NAN_H;ABACUS_NAN;ABACUS_NAN")
set(max "0x7f;0xffu;0x7fff;0xffffu;0x7fffffff;0xffffffffu;0x7fffffffffffffff;0xffffffffffffffffu;ABACUS_NAN_H;ABACUS_NAN;ABACUS_NAN")
set(type_guards_start ";;;;;;;;#ifdef __CA_BUILTINS_HALF_SUPPORT;;#ifdef __CA_BUILTINS_DOUBLE_SUPPORT")
set(type_guards_end ";;;;;;;;#endif;;#endif")
set(small_guards_start ";;;;;;;;;#ifdef __CA_BUILTINS_HALF_SUPPORT\n;")
set(small_guards_end ";;;;;;;;;\n#else\ntypedef abacus_void@vector_size@ SmallerType\;\n#endif;")
set(large_guards_start ";;;;;;;;;#ifdef __CA_BUILTINS_DOUBLE_SUPPORT\n;")
set(large_guards_end ";;;;;;;;;\n#else\ntypedef abacus_void@vector_size@ LargerType\;\n#endif;")

set(vector_sizes ";2;3;4;8;16")

foreach(iter RANGE 10)
  foreach(vector_size IN LISTS vector_sizes)
    list(GET types ${iter} type)
    list(GET small ${iter} smaller_type)
    list(GET large ${iter} larger_type)
    list(GET signed ${iter} signed_type)
    list(GET unsigned ${iter} unsigned_type)
    list(GET is_signed ${iter} is_signed_type)
    list(GET is_int ${iter} is_int_type)

    if ("${vector_size}" STREQUAL "")
      set(num_elements 1)
    else()
      set(num_elements ${vector_size})
    endif()

    list(GET min ${iter} min_value)
    list(GET max ${iter} max_value)

    list(GET type_guards_start ${iter} type_guard_start)
    list(GET type_guards_end ${iter} type_guard_end)
    list(GET small_guards_start ${iter} small_guard_start)
    list(GET small_guards_end ${iter} small_guard_end_temp)
    list(GET large_guards_start ${iter} large_guard_start)
    list(GET large_guards_end ${iter} large_guard_end_temp)

    string(CONFIGURE "${small_guard_end_temp}" small_guard_end @ONLY)
    string(CONFIGURE "${large_guard_end_temp}" large_guard_end @ONLY)
    string(CONFIGURE "${main}" processed @ONLY)
    set(generate_input_definitions "${generate_input_definitions}${processed}")
  endforeach()
endforeach()

file(READ "${CMAKE_CURRENT_SOURCE_DIR}/shape.in" shape)
set(mantissa_bits_list "10u;23u;52u")
set(exponent_bits_list "5u;8u;11u")
set(exponent_bias_list "15u;127u;1023u")
set(mantissa_mask_list "0x3FFu;0x007FFFFFu;0x000FFFFFFFFFFFFFu")
set(exponent_mask_list "0x7C00u;0x7F800000u;0x7FF0000000000000u")
set(sign_mask_list "0x8000u;0x80000000u;0x8000000000000000u")
set(inv_sign_mask_list "0x7FFFu;0x7FFFFFFFu;0x7FFFFFFFFFFFFFFFu")
set(ls_exp_bit_list "0x0400u;0x00800000u;0x0010000000000000u")
set(zero_list "0.0f16;0.0f;0.0")
set(zero_point_five_list "0x3800u;0x3F000000u;0x3FE0000000000000u")
set(exponent_ones_list "0x1Fu;0xFFu;0x7FF")
set(nan_list "ABACUS_NAN_H;ABACUS_NAN;ABACUS_NAN")

foreach(iter RANGE 2)
  math(EXPR all_types_iter "${iter} + 8")
  list(GET types ${all_types_iter} type)

  list(GET type_guards_start ${all_types_iter} type_guard_start)
  list(GET type_guards_end ${all_types_iter} type_guard_end)

  list(GET mantissa_bits_list ${iter} mantissa_bits)
  list(GET exponent_bits_list ${iter} exponent_bits)
  list(GET exponent_bias_list ${iter} exponent_bias)
  list(GET mantissa_mask_list ${iter} mantissa_mask)
  list(GET exponent_mask_list ${iter} exponent_mask)
  list(GET sign_mask_list ${iter} sign_mask)
  list(GET inv_sign_mask_list ${iter} inv_sign_mask)
  list(GET ls_exp_bit_list ${iter} ls_exp_bit)
  list(GET zero_list ${iter} zero)
  list(GET zero_point_five_list ${iter} zero_point_five)
  list(GET exponent_ones_list ${iter} exponent_ones)
  list(GET nan_list ${iter} nan)

  string(CONFIGURE "${shape}" processed @ONLY)
  set(generate_input_definitions "${generate_input_definitions}${processed}")
endforeach()

file(READ "${CMAKE_CURRENT_SOURCE_DIR}/abacus_type_traits.in" abacus_type_traits)
string(CONFIGURE "${abacus_type_traits}" abacus_type_traits @ONLY)
file(WRITE "${GENERATE_OUTPUT_FILE}" "${abacus_type_traits}")
