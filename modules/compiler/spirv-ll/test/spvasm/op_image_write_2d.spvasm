; Copyright (C) Codeplay Software Limited
;
; Licensed under the Apache License, Version 2.0 (the "License") with LLVM
; Exceptions; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     https://github.com/codeplaysoftware/oneapi-construction-kit/blob/main/LICENSE.txt
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
; WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
; License for the specific language governing permissions and limitations
; under the License.
;
; SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

; RUN: %if online-spirv-as %{ spirv-as --target-env %spv_tgt_env -o %spv_file_s %s %}
; RUN: %if online-spirv-as %{ spirv-val %spv_file_s %}
; RUN: %pp-llvm-ver -o %t < %s --llvm-ver %LLVMVER
; RUN: spirv-ll-tool -a OpenCL -b 64 -c ImageBasic -c StorageImageWriteWithoutFormat %spv_file_s | FileCheck %t
                            OpCapability Kernel
                            OpCapability Addresses
                            OpCapability ImageBasic
                            OpCapability StorageImageWriteWithoutFormat
                            OpMemoryModel Physical64 OpenCL
                            OpEntryPoint Kernel %image2d "image2d"
                            OpName %image "image"

; All types, variables and constants
                  %void_t = OpTypeVoid
                 %float_t = OpTypeFloat 32
                  %uint_t = OpTypeInt 32 0
                 %v2int_t = OpTypeVector %uint_t 2
               %v4float_t = OpTypeVector %float_t 4
                 %v4int_t = OpTypeVector %uint_t 4
            %image2d_t = OpTypeImage %void_t 2D 0 0 0 0 Unknown WriteOnly
        %image_write_fn_t = OpTypeFunction %void_t %image2d_t
                  %uint_1 = OpConstant %uint_t 1
                 %float_1 = OpConstant %float_t 1
                 %v2int_1 = OpConstantComposite %v2int_t %uint_1 %uint_1
               %v4float_1 = OpConstantComposite %v4float_t %float_1 %float_1 %float_1 %float_1
                %v4uint_1 = OpConstantComposite %v4int_t %uint_1 %uint_1 %uint_1 %uint_1

; All functions
                 %image2d = OpFunction %void_t None %image_write_fn_t                            
                   %image = OpFunctionParameter %image2d_t
; CHECK-GE17: define spir_kernel void @image2d([[TY:target\("spirv.Image", void, 1, 0, 0, 0, 0, 0, 0\)]] noundef %image)
; CHECK-LT17: define spir_kernel void @image2d([[TY:ptr addrspace\(1\)]] noundef %image)
                       %1 = OpLabel
                            OpImageWrite %image %v2int_1 %v4float_1
; CHECK: call spir_func void @_Z12write_imagef11ocl_image2dDv2_iDv4_f([[TY]] %image, <2 x i32> <i32 1, i32 1>, <4 x float> <float 1.000000e+00, float 1.000000e+00, float 1.000000e+00, float 1.000000e+00>)
                            OpImageWrite %image %v2int_1 %v4uint_1
; CHECK: call spir_func void @_Z13write_imageui11ocl_image2dDv2_iDv4_j([[TY]] %image, <2 x i32> <i32 1, i32 1>, <4 x i32> <i32 1, i32 1, i32 1, i32 1>)
                            OpReturn
; CHECK: ret void
                            OpFunctionEnd
; CHECK: }                       
