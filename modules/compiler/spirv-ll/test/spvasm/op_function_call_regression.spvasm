; Copyright (C) Codeplay Software Limited
;
; Licensed under the Apache License, Version 2.0 (the "License") with LLVM
; Exceptions; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     https://github.com/uxlfoundation/oneapi-construction-kit/blob/main/LICENSE.txt
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
; WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
; License for the specific language governing permissions and limitations
; under the License.
;
; SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

; Checks that function calls to forward-referenced functions correctly
; behaves as expected. There was a bug where resolving calls to function forward
; references was leaving behind invalid instructions.

; RUN: %if online-spirv-as %{ spirv-as --target-env %spv_tgt_env -o %spv_file_s %s %}
; RUN: %if online-spirv-as %{ spirv-val %spv_file_s %}
; RUN: spirv-ll-tool -a OpenCL -b 64 -c Addresses %spv_file_s | FileCheck %s

               OpCapability Kernel
               OpCapability Addresses
               OpCapability Int64
               OpCapability Linkage
          %1 = OpExtInstImport "OpenCL.std"
               OpMemoryModel Physical64 OpenCL
               OpSource OpenCL_CPP 100000

               OpName %bar "bar"
               OpName %main "main"
               OpName %foo "foo"
               OpName %a "a"

               OpDecorate %ptr FuncParamAttr ByVal

       %void = OpTypeVoid
      %ulong = OpTypeInt 64 0

     %ptr_ty = OpTypePointer Function %ulong

    %ulong_0 = OpConstant %ulong 0
    %ulong_1 = OpConstant %ulong 1

        %main_fn_ty = OpTypeFunction %void
        %foo_fn_ty = OpTypeFunction %void %ulong %ptr_ty
        %bar_fn_ty = OpTypeFunction %ulong %ulong

%main = OpFunction %void None %main_fn_ty
    %entry_4 = OpLabel
; CHECK: %a = alloca i64, align 8
          %a = OpVariable %ptr_ty Function
; CHECK: [[TMP:%.*]] = call spir_func i64 @bar(i64 0)
      %call0 = OpFunctionCall %ulong %bar %ulong_0
; CHECK: store i64 1, {{(ptr|i64\*)}} %a
               OpStore %a %ulong_1 Aligned 8
; Check we also apply call-site attributes to this function
; CHECK: call spir_func void @foo(i64 [[TMP]], {{(ptr|i64\*)}} byval(i64) align 1 %a)
        %272 = OpFunctionCall %void %foo %call0 %a
               OpReturn
               OpFunctionEnd


; CHECK: define private spir_func void @foo(i64 {{%.*}}, {{(ptr|i64\*)}} byval(i64) align 1 {{%.*}}) #0 {
%foo = OpFunction %void None %foo_fn_ty
  %0 = OpFunctionParameter %ulong
%ptr = OpFunctionParameter %ptr_ty
   %entry_11 = OpLabel
               OpReturn
               OpFunctionEnd


%bar = OpFunction %ulong None %bar_fn_ty
  %2 = OpFunctionParameter %ulong
    %entry_5 = OpLabel
               OpReturnValue %2
               OpFunctionEnd
