; Copyright (C) Codeplay Software Limited
;
; Licensed under the Apache License, Version 2.0 (the "License") with LLVM
; Exceptions; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     https://github.com/codeplaysoftware/oneapi-construction-kit/blob/main/LICENSE.txt
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
; WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
; License for the specific language governing permissions and limitations
; under the License.
;
; SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

; RUN: spirv-ll-tool -a OpenCL -b 64 %spv_file_s -o %t
; RUN: opt -S --passes=always-inline %t -o - | FileCheck -- %s

                    OpCapability Addresses
                    OpCapability Kernel
                    OpCapability Groups

                    OpMemoryModel Physical64 OpenCL


        %uint_ty  = OpTypeInt 32 0
       %uint2_ty  = OpTypeVector %uint_ty 2
          %fcn_ty = OpTypeFunction %uint_ty %uint_ty %uint2_ty
%work_group_scope = OpConstant %uint_ty 2

                    OpName %broadcast_val "val"
                    OpName %broadcast_fcn "broadcast"
                    OpName %xy "xy"

; CHECK: define private spir_func i32 @broadcast(i32 [[VAL:%.*]], <2 x i32> [[XY:%.*]])
; CHECK:  [[X:%.*]] = extractelement <2 x i32> [[XY]], i64 0
; CHECK:  [[Y:%.*]] = extractelement <2 x i32> [[XY]], i64 1
; CHECK:  [[Xi64:%.*]] = zext i32 [[X]] to i64
; CHECK:  [[Yi64:%.*]] = zext i32 [[Y]] to i64
; CHECK: [[RES:%.*]] = call spir_func i32 @_Z20work_group_broadcastjmm(i32 [[VAL]], i64 [[Xi64]], i64 [[Yi64]])
; CHECK: ret i32 [[RES]]
%broadcast_fcn = OpFunction %uint_ty None %fcn_ty
%broadcast_val = OpFunctionParameter %uint_ty
           %xy = OpFunctionParameter %uint2_ty
 %broadcast_bb = OpLabel
%broadcast_res = OpGroupBroadcast %uint_ty %work_group_scope %broadcast_val %xy
                 OpReturnValue %broadcast_res
                 OpFunctionEnd
